plugins {
    id 'com.github.johnrengelman.shadow'
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

repositories {
    maven {
        name = 'NeoForged'
        url = 'https://maven.neoforged.net/releases'
    }
}

dependencies {
    neoForge "net.neoforged:neoforge:$rootProject.neoforge_version"

    modImplementation "dev.architectury:architectury-neoforge:$rootProject.architectury_api_version"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionNeoForge')

    modApi("dev.ftb.mods:ftb-library-neoforge:${rootProject.ftb_lib}") { transitive false }
    modApi "curse.maven:sdm-ui-lib-1095061:6515615"


    minecraftClientLibraries("io.github.spair:imgui-java-lwjgl3:${rootProject.imguiVersion}")
    minecraftClientLibraries("io.github.spair:imgui-java-binding:${rootProject.imguiVersion}")
    minecraftClientLibraries("io.github.spair:imgui-java-natives-windows:${rootProject.imguiVersion}")
    minecraftClientLibraries("io.github.spair:imgui-java-natives-linux:${rootProject.imguiVersion}")
    minecraftClientLibraries("io.github.spair:imgui-java-natives-macos:${rootProject.imguiVersion}")

    modImplementation "curse.maven:sdm-economy-1102542:6770946"
    modImplementation "curse.maven:crafttweaker-239197:6434161"
}

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/neoforge.mods.toml') {
        expand version: project.version
    }
}

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
